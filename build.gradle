buildscript {
    repositories {
        jcenter()
    }
}

allprojects {
    group = 'com.ocadotechnology.newrelicalertsconfigurator'
    version = getPublicationVersion()

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
}

private def getPublicationVersion() {
    if (project.hasProperty('build_version')) {
        def version = project.getProperty('build_version')
        if (version) {
            return version
        }
    }
    return '3.4.0-SNAPSHOT'
}

subprojects {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.16.16'
        compile 'org.slf4j:slf4j-api:1.7.21'
        testCompile "junit:junit:4.12"
        testCompile 'com.google.guava:guava:20.0'
        testCompile 'org.mockito:mockito-core:1.10.19'
        testCompile 'com.github.tomakehurst:wiremock:2.1.12'
        testCompile 'org.assertj:assertj-core:3.6.1'
    }
}

configure(subprojects - project(':newrelic-alerts-configurator-examples')) {
    if(project.hasProperty('release')) {
        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        artifacts {
            archives javadocJar, sourcesJar
        }
    
        signing {
            required { !version.endsWith("-SNAPSHOT") && gradle.taskGraph.hasTask("uploadArchives") }
            sign configurations.archives
        }

        uploadArchives {
            doFirst {
                println "Uploading archive ${project.name} in version ${version}"
            }

            repositories {
                mavenDeployer {
                
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                        authentication(userName: ossrhUsername, password: ossrhPassword)
                    }

                    pom.project {
                        name 'NewRelic Alerts Configurator'
                        packaging 'jar'
                        // optionally artifactId can be defined here 
                        description 'NewRelic Alerts Configurator can be used to configure NewRelic alerts for your application. Instead of defining alerts through UI you can define them in code. It allows you to automatize alerts configuration, easily recover them in case of wipe out and have full history of changes in your version control system.'
                        url 'https://github.com/ocadotechnology/newrelic-alerts-configurator'

                        scm {
                            connection 'scm:git:git://github.com/ocadotechnology/newrelic-alerts-configurator.git'
                            developerConnection 'scm:git:git@github.com:ocadotechnology/newrelic-alerts-configurator.git'
                            url 'https://github.com/ocadotechnology/newrelic-alerts-configurator'
                        }

                        licenses {
                            license {
                                name 'The Apache License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            }
                        }
                        organization {
                            name 'Ocado Technology'
                            url 'http://www.ocadotechnology.com/'
                        }
                        developers {
                            developer {
                                id 'panda.team'
                                name 'Panda Team'
                                email 'panda.team@ocado.com'
                            }
                        }
                    }
                }
            }
        }
    }
}

project(':newrelic-api-client') {
    dependencies {
        compile 'commons-io:commons-io:2.5'
        compile 'org.apache.commons:commons-lang3:3.5'
        compile 'org.glassfish.jersey.core:jersey-client:2.24.1'
        compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.24.1'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.8.4'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.8.4'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.4'
    }
}

project(':newrelic-alerts-configurator') {
    dependencies {
        compile project(':newrelic-api-client')
    }
}

project(':newrelic-alerts-configurator-examples') {
    dependencies {
        compile project(':newrelic-alerts-configurator')
    }
}
